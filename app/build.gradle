apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        maven { url 'https://maven.google.com' }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        // These docs use an open ended version so that our plugin
        // can be updated quickly in response to Android tooling updates

        // We recommend changing it to the latest version from our changelog:
        // https://docs.fabric.io/android/changelog.html#fabric-gradle-plugin
        classpath "io.fabric.tools:gradle:$ver_fabric_gradle"
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.mw.beam.beamwallet"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 48
        versionName "2.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    androidExtensions {
        experimental = true
    }

    flavorDimensions "api"

    productFlavors {
        mainnet {
            dimension "api"
            applicationIdSuffix ".mainnet"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-mainnet.pro'
        }
        masternet {
            dimension "api"
            applicationIdSuffix ".masternet"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-masternet.pro'
        }
        beamtestnet {
            dimension "api"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-testnet.pro'
        }
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

crashlytics {
    enableNdk true
}

tasks.whenTaskAdded { task ->
    if (task.name == 'assembleMasternetDebug' || task.name == 'assembleMasternetRelease') {
        task.dependsOn updateMasternetLibs
    }
    if (task.name == 'assembleBeamtestnetDebug' || task.name == 'assembleBeamtestnetRelease') {
        task.dependsOn updateTestnetLibs
    }
    if (task.name == 'assembleMainnetDebug' || task.name == 'assembleMainnetRelease') {
      //  task.dependsOn updateMainnetLibs
    }
}

task updateMasternetLibs(type: Exec) {
    doFirst {
        workingDir "$rootDir"
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
          //  commandLine 'cmd', '/c', file("$rootDir/android-wallet-masternet.bat").absolutePath
        } else {
            exec {
                commandLine 'chmod', '+x', file("$rootDir/android-wallet-masternet.sh").absolutePath
            }
            commandLine "$rootDir/android-wallet-masternet.sh"
        }
    }
}

task updateTestnetLibs(type: Exec) {
    doFirst {
        workingDir "$rootDir"
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
            //  commandLine 'cmd', '/c', file("$rootDir/android-wallet-testnet.bat").absolutePath
        } else {
            exec {
                commandLine 'chmod', '+x', file("$rootDir/android-wallet-testnet.sh").absolutePath
            }
            commandLine "$rootDir/android-wallet-testnet.sh"
        }
    }
}

task updateMainnetLibs(type: Exec) {
    doFirst {
        workingDir "$rootDir"
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
            //  commandLine 'cmd', '/c', file("$rootDir/android-wallet-mainnet.bat").absolutePath
        } else {
            exec {
                commandLine 'chmod', '+x', file("$rootDir/android-wallet-mainnet.sh").absolutePath
            }
            commandLine "$rootDir/android-wallet-mainnet.sh"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$ver_kotlin"
    implementation "com.android.support:appcompat-v7:$ver_support"
    implementation "com.android.support:support-v4:$ver_support"
    implementation "com.android.support:support-media-compat:$ver_support"
    implementation "com.android.support.constraint:constraint-layout:$ver_constraint_layout"
    implementation "com.android.support:recyclerview-v7:$ver_support"
    implementation "com.android.support:design:$ver_support"
    implementation "com.android.support:cardview-v7:$ver_support"
    implementation "io.reactivex.rxjava2:rxandroid:$ver_rx_android"
    implementation "io.reactivex.rxjava2:rxjava:$ver_rx_java"
    implementation "com.github.restorer:holycycle:$ver_holycycle"
    implementation "com.google.firebase:firebase-core:$ver_firebase_core"
    implementation "com.elvishew:xlog:$ver_xlog"
    implementation "com.google.zxing:core:$ver_qr"
    implementation "com.journeyapps:zxing-android-embedded:$ver_qr_android_embedded"
    implementation "online.devliving:securedpreferencestore:$ver_secured_store"
    implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:$ver_keyboard_event"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$ver_leak_canary"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$ver_leak_canary"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$ver_leak_canary"
    implementation "com.google.code.gson:gson:$ver_gson"

    testImplementation "junit:junit:$ver_junit"
    testImplementation "org.powermock:powermock-module-junit4:$ver_powermock"
    testImplementation "org.powermock:powermock-module-junit4-rule:$ver_powermock"
    testImplementation "org.powermock:powermock-api-mockito:$ver_powermock"
    testImplementation "org.powermock:powermock-classloading-xstream:$ver_powermock"
    androidTestImplementation "com.android.support.test:runner:$ver_test_runner"
    androidTestImplementation "com.android.support.test:rules:$ver_test_rules"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$ver_espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:$ver_espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$ver_espresso"
    androidTestImplementation "com.agoda.kakao:kakao:$ver_kakao"
    implementation("com.crashlytics.sdk.android:crashlytics-ndk:$ver_crashlytics_ndk") {
        transitive = true
    }
    implementation("com.crashlytics.sdk.android:crashlytics:$ver_crashlytics") {
        transitive = true
    }
}

apply plugin: 'com.google.gms.google-services'
